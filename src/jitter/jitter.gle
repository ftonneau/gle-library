! Example with jittering.
! Author: Francois Tonneau

! This example shows how the 'let dn = ...' command can be combined with the
! rnd() function to add jittering to data.

size 12 7

set font ss

marker_size = 0.2

sub jitter amount
    return rnd(amount)-amount/2                 ! => centered, uniform noise
end sub

sub plot_panel title$ mode$
    begin graph
        size 4 5
        title title$ hei 0.35 dist 0.5
        xaxis min 0.5 max 2.5 ftick 1 dticks 1
        xside off
        xticks off
        xtitle "Type of supplement" hei 0.3 dist 0.5
        xnames "OJ" "VC"
        x2axis off
        yaxis min 0 max 40 ftick 0 dticks 10
        yticks length -0.15
        ytitle "Length of odontoblasts (\sethei{.37}\mu \sethei{.3}m)" &
            hei 0.3 dist 0.5
        y2axis off
        data "jitter.dat" d1=c1,c2 d2=c3,c4
        if (mode$ = "jitter") then
            !
            ! Here we use horizontal jittering. To add vertical jittering (not
            ! recommended in this case), one might write:
            !
            ! let d1 = x+jitter(0.2), d1+jitter(10)
            ! let d2 = x+jitter(0.2), d2+jitter(10)
            !
            let d1 = x+jitter(0.2), d1
            let d2 = x+jitter(0.2), d2
        end if
        d1 marker circle msize marker_size
        d2 marker circle msize marker_size
    end graph
end sub

sub add_means
    amove xg(0.75) yg(20.66)
    aline xg(1.25) yg(20.66)
    !
    amove xg(1.75) yg(16.96)
    aline xg(2.25) yg(16.96)
end sub

! panel without jittering

amove 1.5 1
plot_panel "RAW DATA" "raw"
add_means

! panel with jittering

amove 7.5 1

plot_panel "JITTERING" "jitter"
add_means

