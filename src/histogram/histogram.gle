! Example of circular histogram.
! Author: Francois Tonneau

size 18 11

include pole.gle

set font texcmss hei 0.3

color_1$ = "#daa520"
color_2$ = "#005d67"

wheelcenter 6.5 5.5
wheeldims 2 3.5
wheelvalues 0 100

startangles at 108 going clockwise

drawpolarhist "histogram.dat" width 3.2 direction outward

set color white lwidth 0.02
drawsomerings 25 50 75

set color gray70

labelstyle dist 0.3 just radial
writelabelsfrom "histogram.dat"

set font ss hei 0.3

tagstyle xoffset -0.4 yoffset 0.1 just tc format "fix 0 append %"
writesometags -2 25 50 75

labelstyle dist 1.3 just basic
writelabelsfrom "histlabels.dat"

set hei 0.4
amove 12 9
write "Allelic imbalance:"
begin key
    absolute 12 7.5 nobox dist 0.3
    marker fsquare msize 0.25 color color_1$ text "Non-rearranged 3q allele"
    marker fsquare msize 0.25 color color_2$ text "Rearranged 3q allele"
end key

