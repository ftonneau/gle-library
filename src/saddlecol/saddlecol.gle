! Colored mesh of saddle function.
! Author: Francois Tonneau

include s3.gle
include palettes.gle

size 16 15

xrange 0 15
yrange 0 20
zrange 0 3

angles 40 35
persp 0.80

viewport 10 9

amove 3 3

set cap round join round

begin origin

! Draw plot panes.

set lwidth 0.03 color gray40 fill "#e0e0e0"
pane B
grid xB 0 15 5
grid yB 0 20 5
pane I
grid xI 0 15 5
grid zI 1 3 1
pane J
grid yJ 0 20 5
grid zJ 1 3 1

! Add axis labels.

set color black font ss hei 0.4
set just cr
label XB 0 0.5 0
label XB 5 0.5 5
label XB 10 0.5 10
label XB 15 0.5 15

set just cc
label YB 0 0.5 0
label YB 5 0.5 5
label YB 10 0.5 10
label YB 15 0.5 15
label YB 20 0.5 20
set just rc
label JX  1 0.3 "+1.0"
label JX  2 0.3 "+2.0"
label JX  3 0.3 "+3.0"

! Add axis titles.

set hei 0.5
label XB 8   1.7 "X"
label YB 10  1.8 "Y"
label JX 1.8 1.9 "Z"

! Proceed to plot.

sub sincos x y
    return sin(x)*cos(y)
end sub

sub saddle x y
    return 3/2 * (cos(3/5*(y-1))+5/4)/(1+(((x-4)/3)^2))
end sub

sub fillpalette z
    return plasma(z / 5)
end sub

sub linepalette z
    return "#aaaaaa"
end sub

set lwidth 0.02
s3funcsurf saddle fillpalette linepalette 50 50

end origin

