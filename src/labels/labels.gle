! Example with labeled bars.
! Author: Francois Tonneau

include graphutil.gle

size 17 11

set font ss

amove 2.5 2
begin graph
    size 13 7
    fullsize
    xaxis min 0 max 16 ftick 0 dticks 1 nolast
    yaxis min 0 max 40000 ftick 0 dticks 10000 grid color white lwidth 0.03
    side   off
    xticks off
    ynames 0 10k 20k 30k 40k
    labels color black dist 0.3
    xtitle "Recording time (s)" hei 0.45 dist 0.4
    ytitle "Number of recordings" hei 0.45 dist 0.5
    data "labels.dat"
    begin layer 150
        bar d1 width 1 color white lwidth 0.03 fill #37428C
    end layer
end graph

! We add bar labels. Each label is prettified with the 'grouped$' subroutine
! defined in strings.gle. The number of digits per group is 3, a comma is the
! group separator.

include strings.gle

set hei 0.3 color #37428C

sub put_number index dx dy angle
    local x = dataxvalue(d1, index)
    local y = datayvalue(d1, index)
    local label$ = grouped$(num$(y), 3, ",")
    amove xg(x)+dx yg(y)+dy
    begin rotate angle
        write label$
    end rotate
end sub

put_number 1 -0.6 0.15 0

for num = 2 to ndata(d1)
    put_number num -0.1 0.1 25
next num

! We add a plot title and the missing last tick.

set color black just tc hei 0.6

amove 10 8
write "All results"

set hei 0.4

amove xg(16) yg(0)-0.3
write "\infty"

